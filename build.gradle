plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'nebula.maven-publish' version '5.1.0'
    id 'nebula.javadoc-jar' version '5.1.0'
}

group 'de.uni-passau.fim.sommercamp'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url './libs'
        }
    }
}

dependencies {
    compile group: 'com.github.ocraft', name: 'ocraft-s2client', version: '0.1.5-SNAPSHOT'
    compile group: 'com.intellij', name: 'forms_rt', version: '182.3684.40'

    compile group: 'args4j', name: 'args4j', version: '2.33'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.11'

    testCompile 'junit:junit:4.12'
}

sourceSets {
    main {
        java {
            srcDir 'src/java/'
            exclude '**/bots/**'
        }
    }
}

jar {
    manifest {
        attributes(
            'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
            'Main-Class': "de.uni_passau.fim.sommercamp.sc2.Main"
        )
    }

    into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForNebulaPublication }
        rename ".*", "pom.xml"
    }
}

publishing {
    repositories {
        maven {
            name "projectIncludes"
            url "../SC-Bots/libs"
        }
    }
}

startScripts {
    applicationName = rootProject.name
    mainClassName = "de.uni_passau.fim.sommercamp.sc2.Main"
}
